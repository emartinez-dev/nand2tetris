// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // The first part of the address selects the RAM8 bank, 
    // and the second part is the RAM8 address 

    DMux8Way(in=load, sel=address[3..5],
        a=ram1, b=ram2, c=ram3, d=ram4, e=ram5, f=ram6, g=ram7, h=ram8);

    RAM8(in=in, load=ram1, address=address[0..2], out=outram1);
    RAM8(in=in, load=ram2, address=address[0..2], out=outram2);
    RAM8(in=in, load=ram3, address=address[0..2], out=outram3);
    RAM8(in=in, load=ram4, address=address[0..2], out=outram4);
    RAM8(in=in, load=ram5, address=address[0..2], out=outram5);
    RAM8(in=in, load=ram6, address=address[0..2], out=outram6);
    RAM8(in=in, load=ram7, address=address[0..2], out=outram7);
    RAM8(in=in, load=ram8, address=address[0..2], out=outram8);

    Mux8Way16 (a=outram1, b=outram2, c=outram3, d=outram4, e=outram5, f=outram6, g=outram7, h=outram8,
        sel=address[3..5], out=out);
}